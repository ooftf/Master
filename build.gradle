buildscript {
    ext.hilt_version = "2.38.1"
    ext.isMaven = { name ->
        Properties properties = new Properties()
        def file = rootProject.file("${name}/maven.properties")
        if (file.exists()) {
            InputStream inputStream = file.newDataInputStream()
            properties.load(inputStream)
            def str = properties.getProperty('MAVEN')
            if (str == null) {
                return false
            } else {
                return Boolean.parseBoolean(str)
            }
        }
        return false
    }
    ext.mavenVersion = { name ->
        println("mavenVersion::${name}")
        Properties properties = new Properties()
        def file = rootProject.file("${name}/maven.properties")
        if (file.exists()) {
            InputStream inputStream = file.newDataInputStream()
            properties.load(inputStream)
            def str = properties.getProperty('VERSION')
            if (str == null) {
                throw Exception(file.path + "    VERSION == null")
            } else {
                return str
            }
        }
        return ""
    }
    ext.projectCompat = { name ->
        def realName = name.replace(":", "")
        if (isMaven(realName)) {
            return "com.ooftf.project.master:${realName}:${mavenVersion(realName)}"
        } else {
            return project(name)
        }
    }
    ext {
        versions = [
                'compileSdk' : 30,
                'minSdk'     : 21,
                'targetSdk'  : 26,
                'butterknife': "10.1.0",
                'tinker'     : "1.9.9",
                'tinkerpatch': "1.2.9",
                'packerNG'   : "2.0.1",
                'glide'      : "4.11.0",
                'java'       : JavaVersion.VERSION_1_8,
                'rxlifecycle': "4.0.1",
                'master'     : "1.0.1-SNAPSHOT"
        ]

        deps = [
                'fastjson'                         : 'com.alibaba:fastjson:1.1.71.android',
                'HiHttp'                           : 'com.ooftf:hi-http:4.2.4',
                'MMKV'                             : 'com.tencent:mmkv-static:1.2.7',
                'progressDrawable'                 : 'com.github.ooftf:progress-drawable:1.2.0',
                'disklrucache'                     : 'com.jakewharton:disklrucache:2.0.2',
                'bugly'                            : 'com.tencent.bugly:crashreport_upgrade:latest.release',
                'matisse'                          : 'com.zhihu.android:matisse:0.5.2-beta4',
                'CalendarView'                     : 'com.github.ooftf:CalendarView:1.0.5',
                'ARouterCompiler'                  : "com.alibaba:arouter-compiler:1.5.1",
                'ARouterApi'                       : 'com.alibaba:arouter-api:1.5.1',
                'bindingcollectionadapter'         : 'me.tatarka.bindingcollectionadapter2:bindingcollectionadapter:3.1.1',
                'bindingcollectionadapter_recycler': 'me.tatarka.bindingcollectionadapter2:bindingcollectionadapter-recyclerview:3.1.1',
                'SmartRefreshLayout'               : 'com.scwang.smartrefresh:SmartRefreshLayout:1.1.0-andx-8',
                'SmartRefreshHeader'               : 'com.scwang.smartrefresh:SmartRefreshHeader:1.1.0-andx-8',
                'material'                         : 'com.google.android.material:material:1.3.0',
                'banner'                           : 'io.github.youth5201314:banner:2.2.2',
                'kvLayout'                         : 'com.github.ooftf:kv-layout:1.0.9',
                'imagePreview'                     : 'com.github.ooftf:image-preview:2.3.1',
                'basic'                            : 'com.github.ooftf:basic:0.4.2',
                'afm'                              : 'com.github.ooftf:arch-frame-mvvm:0.2.2',
                'x5Webview'                        : 'com.tencent.tbs.tbssdk:sdk:43993',
                'statelayout'                      : 'com.github.ooftf:master-widget-statelayout:1.1.3',
                'toolbar'                          : 'com.github.ooftf:master-widget-toolbar:1.1.8',
                'dataBindingEx'                    : 'com.github.ooftf:dataBinding-extensions:1.2.6',
                'tablayoutPro'                     : 'com.github.ooftf:tablayout-pro:0.0.3',
                'dialog'                           : 'com.github.ooftf:master-widget-dialog:1.2.4',
                'HpptUiMapping'                              : 'com.github.ooftf:http-ui-mapping:1.4.2'
        ]

        androidx = [
                'constraintLayout': 'androidx.constraintlayout:constraintlayout:2.0.4',
                'appcompat'       : 'androidx.appcompat:appcompat:1.2.0',
                'vectordrawable'  : 'androidx.vectordrawable:vectordrawable:1.1.0-beta02',
                'cardview'        : 'androidx.cardview:cardview:1.0.0',
                'recyclerview'    : 'androidx.recyclerview:recyclerview:1.2.0',
                'legacy_v4'       : 'androidx.legacy:legacy-support-v4:1.0.0',
                'legacy_v13'      : 'androidx.legacy:legacy-support-v13:1.0.0',

        ]
        test = [
                'junit'   : 'junit:junit:4.12',
                'runner'  : 'androidx.test:runner:1.2.0',
                'espresso': 'androidx.test.espresso:espresso-core:3.2.0',
        ]
        versions.kotlin = '1.5.21'
    }
    repositories {
        jcenter()
        mavenCentral()
        google()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        mavenLocal()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        // classpath "com.tinkerpatch.sdk:tinkerpatch-gradle-plugin:$versions.tinkerpatch"
        classpath ("com.tencent.matrix:matrix-gradle-plugin:${MATRIX_VERSION}") { changing = true }
        classpath "com.mcxiaoke.packer-ng:plugin:$versions.packerNG"
        classpath "com.alibaba:arouter-register:1.0.2"
        classpath 'com.github.ooftf:autoregister:1.4.4'
        classpath "io.github.didi.dokit:dokitx-plugin:3.4.3.1"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hilt_version"

    }

}

allprojects {
    repositories {
        mavenCentral()
        jcenter()
        google()
        maven { url 'https://jitpack.io' }
        mavenLocal()
        maven() {
            url "https://oss.sonatype.org/content/repositories/snapshots"
        }
        maven {
            credentials {
                username '5f0eaf7299d2094b1a8c6600'
                password 'JWiB]Owny)E7'
            }
            url 'https://packages.aliyun.com/maven/repository/2017033-release-61klBZ/'
        }
        maven {
            credentials {
                username 'XeiNW4'
                password 'RR3XPNBSd2'
            }
            url 'https://repo.rdc.aliyun.com/repository/78416-release-TOhjiY/'
        }
        maven {
            credentials {
                username 'XeiNW4'
                password 'RR3XPNBSd2'
            }
            url 'https://repo.rdc.aliyun.com/repository/78416-snapshot-YPE3KZ/'
        }
        maven {url 'http://developer.huawei.com/repo/'}
    }
    configurations.all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            if (details.requested.group == 'com.ooftf') {
                if (details.requested.name == 'http-ui-mapping') {
                    details.useTarget(deps.HpptUiMapping)
                } else if (details.requested.name == 'arch-frame-mvvm') {
                    details.useTarget(deps.afm)
                } else if (details.requested.name == 'basic') {
                    details.useTarget(deps.basic)
                } else if (details.requested.name == 'master-widget-statelayout') {
                    details.useTarget(deps.statelayout)
                } else if (details.requested.name == 'master-widget-dialog') {
                    details.useTarget(deps.dialog)
                } else if (details.requested.name == 'ImagePreview') {
                    details.useTarget(deps.imagePreview)
                } else if (details.requested.name == 'kv-layout') {
                    details.useTarget(deps.kvLayout)
                } else if (details.requested.name == 'dataBinging-extensions') {
                    details.useTarget(deps.dataBindingEx)
                } else if (details.requested.name == 'tableview') {
                    details.useTarget('com.github.ooftf:tableview:0.8.9.3.1')
                } else if (details.requested.name == 'master-widget-toolbar') {
                    details.useTarget(deps.toolbar)
                } else if (details.requested.name == 'JLog') {
                    details.useTarget('com.github.ooftf:JLog:1.0.0')
                } else if (details.requested.name == 'gson-adapter-extensions') {
                    details.useTarget('com.github.ooftf:gson-adapter-extensions:0.0.4')
                } else if (details.requested.name == 'hi-http') {
                    details.useTarget(deps.HiHttp)
                }
            } else if (details.requested.group == 'com.github.ybq' && details.requested.name == 'Android-SpinKit') {
                details.useTarget('com.github.ooftf:loading-view:0.0.1')
            } else if (details.requested.group == 'com.github.ooftf' && details.requested.name == 'dataBinging-extensions') {
                details.useTarget(deps.dataBindingEx)
            } else if (details.requested.group == 'com.github.razerdp' && details.requested.name == 'BasePopup') {
                details.useTarget('io.github.razerdp:BasePopup:3.1.4')
            } else if (details.requested.group == 'com.youth.banner' && details.requested.name == 'banner') {
                details.useTarget(deps.banner)
            }

        }
    }
}
task clean(type: Delete) {
    delete rootProject.buildDir
}
